//! A simple test library.

let string = import! std.string
let { wrap } = import! std.applicative
let prelude @ { Semigroup } = import! std.prelude
let float = import! std.float
let int = import! std.int
let list @ { List, ? } = import! std.list
let { Foldable, foldl } = import! std.foldable
let { Option } = import! std.option
let { (<>) } = import! std.semigroup
let { error } = import! std.prim
let { id } = import! std.function

let effect @ { Eff, Writer, ? } = import! std.effect


type Test a = Writer (List String) a
type TestEff r a = Eff [ writer : Test | r ] a
type TestCase a =
    | Test String (() -> Eff [ writer : Test ] a)
    | Group String (Array (TestCase a))

let test = Test
let group = Group

let assert x = if x then () else error "Assertion failed"

let assert_eq l r : [Show a] -> [Eq a] -> a -> a -> Eff [ writer : Test | r ] () =
    if l == r
    then wrap ()
    else effect.tell (Cons ("Assertion failed: " <> show l <> " != " <> show r) Nil)

let assert_neq l r : [Show a] -> [Eq a] -> a -> a -> Eff [ writer : Test | r ] () =
    if l /= r
    then wrap ()
    else effect.tell (Cons ("Assertion failed: " <> show l <> " == " <> show r) Nil)

rec let run_raw test : Eff [ writer : Test | r ] a -> List String =
    let test = effect.run_pure (effect.run_writer test)
    test.writer

rec let run test : Eff [ writer : Test | r ] a -> () =
    let writer = run_raw test
    match writer with
    | Cons _ _ -> error (foldl (\acc err -> acc <> "\n" <> err) "" writer)
    | Nil -> ()
in

{
    Test,
    TestEff,
    TestCase,

    test,
    group,

    assert,
    assert_eq,
    assert_neq,

    run_raw,
    run,
}
